#!/usr/bin/env ys-0

use: 'read-config'

config =: read-config('config.yaml')

defn main():
  :: Generate a markdown page comparing YAMLScript's Exercism solutions to
     those in other languages.

  print: |
    Comparing YAMLScript Programs to Other Languages
    ================================================

    This page compares some YAMLScript programs from the upcoming
    [YAMLScript Exercism Track](https://exercism.org/tracks/yamlscript)
    with other Exercism language solutions.

    This markdown page was generated by a [YAMLScript program](
    https://github.com/ingydotnet/yamlscript-vs/blob/main/bin/ysvs).

    $format-body()

defn format-body():
  reduce _ '' config.progs:
    fn(markdown prog):
      str markdown: |

        ----

        ## $(prog.name)

        $format-info(prog)

        ```yaml
        $yamlscript-code(prog)
        ```

        <ul>
        $(format-prog(prog))
        </ul>

defn format-info(prog):
  slug =: prog.slug
  intro =: "exercism/yamlscript/exercises/practice/$slug/.docs/introduction.md"
  instr =: "exercism/yamlscript/exercises/practice/$slug/.docs/instructions.md"
  str:
    when fs-f(intro): |

      $(slurp(intro)
        .replace(_ "# Introduction\n\n" '')
        .replace(_ "# Description\n\n" ''))

defn yamlscript-code(prog):
  slug =: prog.slug
  slurp: "exercism/yamlscript/exercises/practice/$slug/.meta/$slug.ys"

defn format-prog(prog):
  reduce _ '' config.langs:
    fn(markdown lang):
      path =: lang.path.replace(_ 'SLUG' prog.slug)
      str markdown:
        if fs-f(path):
          format-prog-lang: lang path
          warn: "Not found: $path"

defn format-prog-lang(lang path): |
  <details><summary>$(lang.name)</summary>

  ```$(lang.slug)
  $slurp(path)
  ```

  </details>
